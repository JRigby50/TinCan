'Script to setup a Print Server
'Written by Jim Rigby, james.rigby@ngc.com
'Created 3/13/13
'Last updated  5/20/14 to speed up detection of existing Ports and Queues
'Works with the .csv files that are generated by either MigrationPrep.vbs or BkUpPrtSvr.vbs
'The SNMP Column must be added to the file generated by BkUpPrtSvr.vbs
'Currently the InstallDrivers Subroutine is not complete and is not implemented.
'This means that the Printer Drivers need to be installed manually before this script will work.
Option Explicit
Dim bolDriver
Dim objFSO, objInputFile, objWMIService, objPort, objPrinter
Dim arrPrinter
Dim strComputer, strPrinterName, strPortName, strDriverName, strComment, strLocation, strSNMP
Dim intResult
Const ForReading = 1, ForWriting = 2
Const wbemFlagReturnImmediately = &h10
Const wbemFlagForwardOnly = &h20
strComputer = "."
Set objFSO = CreateObject("Scripting.FileSystemObject")
Set objInputFile = objFSO.OpenTextFile(".\Printers.csv", ForReading)
Set objWMIService = GetObject("winmgmts:{impersonationLevel=impersonate}!\\" & strComputer & "\root\cimv2")
'*****Start Installing*****
'Read the Input File and create the Ports and Queues
While Not objInputFile.AtEndOfStream
arrPrinter = split(objInputFile.Readline, ",")
strPrinterName = arrPrinter(0)
strPortName = arrPrinter(1)
strDriverName = arrPrinter(2)
strComment = arrPrinter(3)
strLocation = arrPrinter(4)
strSNMP = arrPrinter(5)
'Skip the first row
If strPrinterName <> "Printer Name" Then
'If the Port does not Exist, Create it
	If CheckForPort=False Then
	CreatePort
	End If
'If the Queue does not Exist, Create it
	If CheckForQueue=False Then
	CreateQueue
	End If
End If
Wend
MsgBox "Print Server Setup Complete"
'*****
'Functions And Subroutines
'*****
Function CheckForPort()
'See if the Port already exists and set bolPort True if it does
Dim colPorts, objName, bolPort
bolPort = False
Set colPorts = objWMIService.ExecQuery("SELECT * FROM Win32_TCPIPPrinterPort",,48)' "WQL", wbemFlagReturnImmediately + wbemFlagForwardOnly)**Where name='" & strPortName & "'",,48)
	For Each objName In colPorts
		If strPortName = objName.Name Then
		bolPort = True
		CheckForPort = bolPort
		Exit Function
		End If
	Next
CheckForPort = bolPort
End Function
'*****
Function CheckForQueue()
Dim colQueues, objQueue, bolQueue
bolQueue = False
'See if the Queue already exists and set bolQueue True if it does
Set colQueues = objWMIService.ExecQuery("Select * from Win32_Printer",,48)'Where name='" & strPrinterName & "'",,48)
'bolQueue = IsEmpty(colQueues)
	For Each objQueue In colQueues
		If strPrinterName = objQueue.Name Then
		bolQueue = True
		CheckForQueue = bolQueue
		Exit Function
		End If
	Next
CheckForQueue = bolQueue
End Function
'*****
Sub CreatePort
Set objPort = objWMIService.Get("Win32_TCPIPPrinterPort").SpawnInstance_
objPort.Name = strPortName
objPort.Protocol = 1
objPort.HostAddress = strPortName
objPort.PortNumber = "9100"
objPort.SNMPEnabled = True
objPort.SNMPCommunity = strSNMP
objPort.Put_
End Sub
'*****
Sub CreateQueue
Set objPrinter = objWMIService.Get("Win32_Printer").SpawnInstance_
objPrinter.DeviceID   = strPrinterName
objPrinter.Comment = strComment
objPrinter.Location = strLocation
objPrinter.DriverName = strDriverName
objPrinter.PortName   = strPortName
objPrinter.Network = True
objPrinter.Shared = True
objPrinter.ShareName = strPrinterName
objPrinter.Put_
End Sub
'*****